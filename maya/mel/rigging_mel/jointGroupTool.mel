//jGrp - group selected joint(s), center group's pivot, and rename "grp_".  NOTE: only works on joints!...

//Find selected joints...
string $selJnts[] = `ls -sl -type joint`;

//Check selection...

if (size($selJnts) == 0){
    warning "Must select a joint!";
} else {
    //For Loop - for joints in selection...
    for ($jnts in $selJnts){
        //Grab each joint...
        select $jnts;
        //Group each joint in selection...
        string $cGrpJnt = `group`;
        //This selects the child (joint)...
        string $nxtJnt[] = `pickWalk -d down`;
        //Create locator.  We use a locator to match the group's pivot with the pivot of the selected joint(s)...
        string $locPs[] = `spaceLocator`;
        //Align the locator to the pivot of the selected joint(s)...
        pointConstraint $nxtJnt[0] $locPs[0];
        //Find the world space position of the locator...
        float $wsSel[] = `xform -q -ws -t $locPs[0]`;
        //Move the pivot of each group to the locator's position...
        xform -ws -piv ($wsSel[0]) ($wsSel[1]) ($wsSel[2]) $cGrpJnt;
        //We can now delete the locator...
        delete $locPs[0];
        //Rename the group to "grp_(joint name)"...
        string $rnmCGrpJnt = `rename $cGrpJnt ("grp_" + $nxtJnt[0])`;
        //Finally, freeze the rotation of each joint.
        makeIdentity -apply 1 -r 1 $nxtJnt[0];
       
    }
}